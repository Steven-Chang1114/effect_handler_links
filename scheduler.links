open import FiberInterface;
open import Queue;

alien javascript "js/runtime.js" {
    sysDequeue: () ~> [a];
}

typename PrioQueue(a) = (high: Queue(a), medium: Queue(a), low: Queue(a), none: Queue(a));
typename SchedulerState(a) = (runQ: PrioQueue(a), prio: Priority, startTime: Int);
typename Fiber(e::Eff) = (prio:Priority, f: (SchedulerState(Fiber({ |e}))) ~e~> ());
typename FiberQueue(e::Eff) = PrioQueue(Fiber({ |e}));

sig priorityEnqueue: (a, Priority, PrioQueue(a)) ~> PrioQueue(a)
fun priorityEnqueue(x, prio, pq){
    switch(prio){
        case High -> (pq with high = enqueue(x, pq.high))
        case Medium -> (pq with medium = enqueue(x, pq.medium))
        case Low -> (pq with low = enqueue(x, pq.low))
        case None -> (pq with none = enqueue(x, pq.none))
    }
}

sig priorityDequeue: (PrioQueue(a)) ~> (Maybe((a, Priority)), PrioQueue(a) )
fun priorityDequeue(pq){
    switch(dequeue(pq.high)){
        case (Just(x), q) -> (Just((x, High)), (pq with high= q))
        case (Nothing,_) ->
            switch(dequeue(pq.medium)){
                case (Just(x), q) -> (Just((x, Medium)), (pq with medium= q))
                case (Nothing,_) ->
                    switch(dequeue(pq.low)){
                        case (Just(x), q) -> (Just((x, Low)), (pq with low=q))
                        case (Nothing, _) -> 
                            switch(dequeue(pq.none)){
                                case (Just(x), q) -> (Just((x, None)), (pq with none=q))
                                case (Nothing, _) -> print("Dequeue empty"); (Nothing, pq)
                            }
                    }
            }
    }
}

sig fiberEnqueue: (Fiber({ |e}), FiberQueue({ |e})) ~> FiberQueue({ |e})
fun fiberEnqueue(f, q){
    priorityEnqueue(f, f.prio, q)
}

sig fiberBulkEnqueue: ([Fiber({ |e})], FiberQueue({ |e})) ~> FiberQueue({ |e})
fun fiberBulkEnqueue(fibers, q){
    switch(fibers){
        case [] -> q
        case x::xs ->
            var pq = fiberEnqueue(x, q);
            fiberBulkEnqueue(xs, pq)
    }
}

sig fiberDequeue: (FiberQueue({ |e})) ~> (Maybe((Fiber({ |e}), Priority)), FiberQueue({ |e}))
fun fiberDequeue(q){
    priorityDequeue(q)
}

fun emptyPrioQueue(){
    (high= emptyQueue(), medium=emptyQueue(), low=emptyQueue(), none=emptyQueue())
}

sig fiberQueueLength: (FiberQueue({ |e}))  -> Int
fun fiberQueueLength(q){
    length(q.low.rear) + length(q.high.front) + length(q.high.rear) + length(q.medium.front) + length(q.medium.rear) + length(q.low.front)
}

#sig schedule: (Fiber0({ |e})) {Yield-, Fork- | e}~> ()
fun schedule(fib){

    #sig makeSchedulerState: (Priority, PrioQueue(a)) ~> SchedulerState(a)
    fun makeSchedulerState(prio, q){
        (runQ = q, prio = prio, startTime = clientTimeMilliseconds())
    }

    fun runNext(q){
        switch(fiberDequeue(q)){
            case (Nothing, _) -> ()
            case (Just((fiber, prio)), q) -> fiber.f(makeSchedulerState(prio, q))
        }
    }

    fun resumptionToFiber(resume, prio){
        (prio = prio, f = fun(state) { resume((), state) })
    }

    fun newPrioToFiber(resume, prio){
        (prio = prio, f = fun(state) { resume((), (state with prio = prio)) })
    }

    mutual {
      #sig fiber0ToFiber: (Fiber0({ |%e})) ~%~> Fiber({ |%e})
      fun fiber0ToFiber(fiber){
          (prio = fiber.prio, f = fun(state) { runFiber(fiber, state.runQ ) })
      }

      #sig poll: (FiberQueue({ |e})) ~> FiberQueue({ |e})
      fun poll(q){
          var enqueue = map(fiber0ToFiber, sysDequeue());
          fiberBulkEnqueue(enqueue, q)
      }

      fun runFiber(fiber, runQ){
          println("runQ: " ^^ intToString(fiberQueueLength(runQ)));
          #dump(self());
          handle(fiber.f()) ( state <- (prio=fiber.prio, runQ=runQ, startTime=clientTimeMilliseconds()) ){
              case x ->
                    print("RETURN");
                    runNext(poll(state.runQ))
              case <GetPrio => resume> -> resume(state.prio, state)
              case <SetPrio(p) => resume> -> 
                    var q = fiberEnqueue(newPrioToFiber(resume, p), state.runQ);
                    runNext(poll(q))
              case <Fork(f) => resume> ->
                    print("fork");
                    var q = poll(state.runQ);
                    # f is new fiber, state.prio is the current fiber
                    switch ((f.prio, state.prio)) {
                      case (High, _) ->
                          # Enqueue the current fiber, run the new fiber
                          print("High fiber enqueued");
                          var qq = fiberEnqueue(resumptionToFiber(resume, state.prio), q);
                          runFiber(f, qq)
                      case (Medium, High) ->
                        print("Medium high fiber enqueued");
                          # Enqueue the new fiber, run the current fiber
                          var qq = fiberEnqueue(fiber0ToFiber(f), q);
                          resume((), (state with runQ = qq))
                      case (Medium, _) ->
                          print("Medium fiber enqueued");
                          # Enqueue the new fiber, run the current fiber
                          var qq = fiberEnqueue(resumptionToFiber(resume, state.prio), q);
                          runFiber(f, qq)
                      case (_, High) ->
                          # Enqueue the new fiber, run the current fiber
                          print("Low High fiber enqueued");
                          var qq = fiberEnqueue(fiber0ToFiber(f), q);
                          resume((), (state with runQ = qq))
                      case (_, Medium) ->
                          # Enqueue the new fiber, run the current fiber
                          print("Low Medium fiber enqueued");
                          var qq = fiberEnqueue(fiber0ToFiber(f), q);
                          resume((), (state with runQ = qq))
                      case (_,_) ->
                          # Enqueue the current fiber, run the new fiber
                          print("Low fiber enqueued");
                          var qq = fiberEnqueue(resumptionToFiber(resume, state.prio), q);
                          runFiber(f, qq)
                    }
              case <Yield => resume> ->
                    #print("YIELD");
                  var currentTime = clientTimeMilliseconds();
                  var buffer = switch(state.prio){
                      case High -> 400
                      case Medium -> 200
                      case Low -> 100
                      case None -> 0
                  };
                  #print("TIME: " ^^ intToString(currentTime - state.startTime) ^^ "," ^^ intToString(buffer));
                  if (currentTime - state.startTime <= buffer) {
                    #print("RESUME"); 
                    resume((), state)
                  } else{
                    #print("SWITCH");
                      var q = fiberEnqueue(resumptionToFiber(resume, state.prio), state.runQ);
                      runNext(poll(q))
                  }
          }
      }
    }

    runFiber(fib, emptyPrioQueue())

}